/*
 * Copyright (c) 2017-2026 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.snowplowanalytics'
archivesBaseName = 'iab-spiders-and-robots-client'
version = '0.2.1-M1'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'org.apache.commons:commons-csv:1.10.0'
    implementation 'commons-io:commons-io:2.15.1'
    implementation 'com.github.seancfoley:ipaddress:5.4.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

def outputDir = layout.projectDirectory.dir("src/main/java/com/snowplowanalytics/iab/spidersandrobotsclient")

tasks.register('generateSources') {
    doFirst {
        def srcFile = outputDir.file("Version.java").asFile
        srcFile.parentFile.mkdirs()
        srcFile.write(
                """/*
 * Copyright (c) 2017-2026 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

package com.snowplowanalytics.iab.spidersandrobotsclient;

// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    static final String CLIENT = "java-${project.version}";
    static final String VERSION = "${project.version}";
}
""")
    }
}

tasks.named('compileJava') {
    dependsOn 'generateSources'
}

tasks.register('printVersion') {
    doLast {
        print project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'iab-spiders-and-robots-client'
                description = 'Snowplow Java client library for the IAB and ABC International Spiders and Robots lists.'
                url = 'https://github.com/snowplow/iab-spiders-and-robots-java-client'
                inceptionYear = '2017'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Snowplow Analytics Ltd'
                        email = 'support@snowplowanalytics.com'
                        organization = 'Snowplow Analytics Ltd'
                        organizationUrl = 'https://snowplow.io'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/snowplow/iab-spiders-and-robots-java-client.git'
                    developerConnection = 'scm:git:ssh://github.com/snowplow/iab-spiders-and-robots-java-client.git'
                    url = 'https://github.com/snowplow/iab-spiders-and-robots-java-client'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("SONA_USER") ?: findProperty("SONA_USER") ?: ""
                password = System.getenv("SONA_PASS") ?: findProperty("SONA_PASS") ?: ""
            }
        }
    }
}

signing {
    def signingKey = System.getenv("PGP_SECRET")
    def signingPassword = System.getenv("PGP_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { System.getenv("PGP_SECRET") != null }
}
