/*
 * Copyright (c) 2017 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'

wrapper.gradleVersion = '3.4.1'

archivesBaseName = 'spiders-and-robots-client'
version = '0.1.0-rc1'

def javaVersion = JavaVersion.VERSION_1_7

repositories {
    // Use 'maven central' for resolving our dependencies
    mavenCentral()
    // Use 'jcenter' for resolving testing dependencies
    jcenter()
}

configure([compileJava, compileTestJava]) {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = 'UTF-8'
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.apache.commons:commons-csv:1.4'
    compile 'commons-net:commons-net:3.6'
    compile 'commons-io:commons-io:2.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:2.6.0'
}

task sourceJar(type: Jar, dependsOn: 'generateSources') {
    from sourceSets.main.allJava
}

task generateSources {
    project.ext.set("outputDir", "$projectDir/src/main/java/com/snowplowanalytics/iab/spidersandrobotsclient")
    doFirst {
        println outputDir
        def srcFile = new File((String) outputDir, "Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
                """/*
 * Copyright (c) 2017 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

package com.snowplowanalytics.iab.spidersandrobotsclient;

// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    static final String CLIENT = "java-$project.version";
    static final String VERSION = "$project.version";
}
""")
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java


task printVersion {
    doLast {
        print "$project.version"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'spiders-and-robots-client'
        pom.version = "$project.version"
        pom.project {
            name = 'spiders-and-robots-client'
            description = 'Snowplow Java client library for the IAB and ABC International Spiders and Robots lists.'
            url = 'https://github.com/snowplow/iab-spiders-and-robots-java-client/'
            inceptionYear = '2017'

            packaging = 'jar'

            groupId = 'com.snowplowanalytics.iab'

            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            scm {
                connection = 'https://github.com/snowplow/iab-spiders-and-robots-java-client.git'
                url = 'https://github.com/snowplow/iab-spiders-and-robots-java-client'
            }
            developers {
                developer {
                    name = 'Snowplow Analytics'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_SNOWPLOW_MAVEN_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_SNOWPLOW_MAVEN_API_KEY')

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'snowplow-maven'
        name = 'spiders-and-robots-client'

        group = 'com.snowplowanalytics'
        userOrg = 'snowplow'

        websiteUrl = 'https://github.com/snowplow/iab-spiders-and-robots-java-client'
        vcsUrl = 'https://github.com/snowplow/iab-spiders-and-robots-java-client'
        issueTrackerUrl = 'https://github.com/snowplow/iab-spiders-and-robots-java-client/issues'

        licenses = ['Apache-2.0']

        version.name = "$project.version"
    }
}